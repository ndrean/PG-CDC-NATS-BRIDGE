defmodule Consumer.CdcConsumer do
  @moduledoc """
  JetStream pull consumer for CDC events from the Zig bridge.

  Consumes CDC events from the CDC_BRIDGE JetStream stream with:
  - Durable consumer tracking
  - Acknowledgment of processed messages
  - Automatic stream/consumer creation
  """
  use Gnat.Jetstream.PullConsumer
  require Logger

  @consumer_name "elixir_cdc_consumer"

  def start_link(consumer_config) do
    Gnat.Jetstream.PullConsumer.start_link(__MODULE__, consumer_config, name: __MODULE__)
  end

  @impl true
  def init(%Gnat.Jetstream.API.Consumer{} = consumer_config) do
    # Get stream name from environment variable or use default
    stream_name = Map.get(consumer_config, :stream_name, "CDC_BRIDGE")
    consumer_name = Map.get(consumer_config, :durable_name, @consumer_name)

    # Ensure NATS connection and stream exists before starting consumer
    with :ok <- ensure_jetstream_enabled(),
         :ok <- ensure_stream_exists(consumer_config),
         :ok <- create_consumer(consumer_config) do
      {:ok, nil, connection_name: :gnat, stream_name: stream_name, consumer_name: consumer_name}
    end
  end

  @impl true
  def handle_message(message, state) do
    try do
      # Decode MessagePack payload
      _decoded = Msgpax.unpack!(message.body)

      # Logger.info("[CDC Consumer] #{message.topic}: #{inspect(decoded)}")

      # Acknowledge successful processing
      {:ack, state}
    rescue
      error ->
        Logger.error("[CDC Consumer] Error processing message: #{inspect(error)}")
        # Negative acknowledgment - will be redelivered
        {:nack, state}
    end
  end

  defp ensure_jetstream_enabled do
    # wait loop for NATS connection establishment
    case Process.whereis(:gnat) do
      nil ->
        Logger.debug("[CDC Consumer] Waiting for NATS connection...")
        Process.send_after(self(), :retry, 100)

        receive do
          :retry ->
            ensure_jetstream_enabled()
        after
          2_000 ->
            Logger.error("[CDC Consumer] Timeout waiting for NATS connection")
            raise "Timeout waiting for NATS connection"
        end

      _pid ->
        # ensure JetStream is enabled by the server
        true = Gnat.server_info(:gnat).jetstream
        Logger.info("[CDC Consumer] â‡ï¸ NATS connection established with JetStream enabled")
        :ok
    end
  end

  defp ensure_stream_exists(%Gnat.Jetstream.API.Consumer{} = consumer_config) do
    stream_name = Map.get(consumer_config, :stream_name, "CDC_BRIDGE")

    # Stream is created by Zig bridge, just verify it exists
    case Gnat.Jetstream.API.Stream.info(:gnat, stream_name) do
      {:ok, _stream_info} ->
        Logger.info("[CDC Consumer] â‡ï¸ Using JetStream stream '#{stream_name}'")
        :ok

      {:error, reason} ->
        Logger.warning(
          "[CDC Consumer] ðŸ”´ Stream '#{stream_name}' not found: #{inspect(reason)}. Make sure Zig bridge is running and has created this exact stream."
        )

        raise "JetStream stream '#{stream_name}' not found"
    end
  end

  defp create_consumer(consumer_config) do
    consumer_name = Map.get(consumer_config, :durable_name)

    case Gnat.Jetstream.API.Consumer.create(:gnat, consumer_config) do
      {:ok, %{created: _}} ->
        Logger.info("[CDC Consumer] â‡ï¸ Durable consumer '#{consumer_name}' created")
        :ok

      {:error, %{"code" => 400, "description" => "consumer name already in use"}} ->
        Logger.info("[CDC Consumer] â‡ï¸ Durable consumer '#{consumer_name}' already exists")
        :ok

      {:error, reason} ->
        Logger.error("[CDC Consumer] ðŸ”´ Failed to create consumer: #{inspect(reason)}")
        raise "Failed to setup JetStream consumer"
    end
  end
end
